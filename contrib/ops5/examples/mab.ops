(literalize monkey
	at
	on
	holds)
; comment between functions

(literalize object
	name
	at
	weight
	on)

(literalize goal
	status
	type
	object
	to)

(p  make-goal==move-ladder-under-bananas
	(goal ^status active ^type holds ^object <w>) ; after ce
	(object ^name <w> ^at <p> ^on ceiling)
    --> ; after arrow
	(make goal ^status active ^type move ^object ladder ^to <p>))


(p make-goal==climb-ladder
	(goal ^status active ^type holds ^object <w>)
	(object ^name <w> ^at <p> ^on ceiling)
	(object ^name ladder ^at <p>)
    -->
	(make goal ^status active ^type on ^object ladder))


(p make-goal==release-hold-on-ladder
	(goal ^status active ^type holds ^object <w>)
	(object ^name <w> ^at <p> ^on ceiling)
	(object ^name ladder ^at <p>)
	(monkey ^on ladder)
    -->
	(make goal ^status active ^type holds ^object nil))


(p action==grab-bananas
	(goal ^status active ^type holds ^object <w>)
	(object ^name <w> ^at <p> ^on ceiling)
	(object ^name ladder ^at <p>)
	(monkey ^on ladder ^holds nil)
    -->
	(write (crlf) grab <w> "	%P%J%J$r0.$l!!")
	(modify 4 ^holds <w>)
	(modify 1 ^status satified))



(p make-goal==walk-to-object
	(goal ^status active ^type holds ^object <w>)
	(object ^name <w> ^at <p> ^on floor)
    -->
	(make goal ^status active ^type walk-to ^object <p>))


(p make-goal==release-at-destination
	(goal ^status active ^type holds ^object <w>)
	(object ^name <w> ^at <p> ^on floor)
	(monkey ^at <p>)
    -->
	(make goal ^status active ^type holds ^object nil))


(p action==pick-up-ladder
	(goal ^status active ^type holds ^object <w>)
	(object ^name <w> ^at <p> ^on floor)
	(monkey ^at <p> ^holds nil)
    -->
	(write (crlf) grab <w> "	2YBf$rDO$a!!")
	(modify 3 ^holds <w>)
	(modify 1 ^status satisfied))

(p make-goal==pick-up-ladder
	(goal ^status active ^type move ^object <o> ^to <p>)
	(object ^name <o> ^weight light ^at <> <p>)
    -->
	(make goal ^status active ^type holds ^object <o>))


(p make-goal==carry-ladder-to-bananas
	(goal ^status active ^type move ^object <o> ^to <p>)
	(object ^name <o> ^weight light ^at <> <p>)
	(monkey ^holds <o>)
    -->
	(make goal ^status active ^type walk-to ^object <p>))


(p goal-satisfied==carry-ladder-to-bananas
	(goal ^status active ^type move ^object <o> ^to <p>)
	(object ^name <o> ^weight light ^at <p>)
    -->
	(modify 1 ^status satisfied))


(p make-goal==jump-onto-floor
	(goal ^status active ^type walk-to ^object <p>)
    -->
	(make goal ^status active ^type on ^object floor))

(p action==walk-to-ladder
	(goal ^status active ^type walk-to ^object <p>)
	(monkey ^on floor ^at {<c> <> <p>} ^holds nil)
    -->
	(write (crlf) walk to <p> "	2YBf$K8~$+$C$FJb$1!!")
	(modify 2 ^at <p>)
	(modify 1 ^status satisfied))


(p action==carry-ladder-to-bananas
	(goal ^status active ^type walk-to ^object <p>)
	(monkey ^on floor ^at {<c> <> <p>} ^holds <w> <> nil)
	(object ^name <w>)
    -->
	(write (crlf) walk to <p> "	2YBf$r%P%J%J$N=j$K1?$Y!!")
	(modify 2 ^at <p>)
	(modify 3 ^at <p>)
	(modify 1 ^status satisfied))

(p action==jump-onto-floor
	(goal ^status active ^type on ^object floor)
	(monkey ^on {<x> <> floor})
    -->
	(write (crlf) jump onto the floor "	>2$K>h$l!!")
	(modify 2 ^on floor)
	(modify 1 ^status satisfied))

(p make-goal==walk-to-ladder
	(goal ^status active ^type on ^object <o>)
	(object ^name <o> ^at <p>)
    -->
	(make goal ^status active ^type walk-to ^object <p>))



(p make-goal==drop-ladder
	(goal ^status active ^type on ^object <o>)
	(object ^name <o> ^at <p>)
	(monkey ^at <p>)
    -->
	(make goal ^status active ^type holds ^object nil))


(p action==climb-onto-ladder
	(goal ^status active ^type on ^object <o>)
	(object ^name <o> ^at <p>)
	(monkey ^at <p> ^holds nil)
    -->
	(write (crlf) climb onto <o> "		2YBf$K>h$l!!")
	(modify 3 ^on <o>)
	(modify 1 ^status satisfied))

(p action==drop-ladder
	(goal ^status active ^type holds ^object nil)
	(monkey ^holds {<x> <> nil})
    -->
	(write (crlf) drop <x> "2YBf$rCV$1")
	(modify 2 ^holds nil)
	(modify 1 ^status satisfied))

(p make-goal==continue
	(goal ^status active)
    -->
	(modify 1 ^status not-processed))

(p make-goal==get-the-bananas
	(start 1)
    -->
	(make monkey ^at 5-7 ^on couch)
	(make object ^name couch ^at 5-7 ^weight heavy)
	(make object ^name bananas ^on ceiling ^at 2-2)
	(make object ^name ladder ^on floor ^at 9-5 ^weight light)
	(make goal ^status active ^type holds ^object bananas))

